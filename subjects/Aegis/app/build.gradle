apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'
apply plugin: 'dagger.hilt.android.plugin'

def getCmdOutput = { cmd ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine cmd
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitHash = { -> return getCmdOutput(["git", "rev-parse", "--short", "HEAD"]) }
def getGitBranch = { -> return getCmdOutput(["git", "rev-parse", "--abbrev-ref", "HEAD"]) }

def packageName = "com.beemdevelopment.aegis"
def fileProviderAuthority = "${packageName}.fileprovider"
def fileProviderAuthorityDebug = "${packageName}.debug.fileprovider"

android {
    compileSdkVersion 33

    namespace packageName

    defaultConfig {
        applicationId "${packageName}"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 58
        versionName "2.1.3"
        multiDexEnabled true
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
        buildConfigField "String", "GIT_BRANCH", "\"${getGitBranch()}\""
        buildConfigField "java.util.concurrent.atomic.AtomicBoolean", "TEST", "new java.util.concurrent.atomic.AtomicBoolean(false)"

        testInstrumentationRunner "com.beemdevelopment.aegis.AegisTestRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'

        unitTests {
            all {
                maxHeapSize "3g"

                failFast false
                ignoreFailures false
                testLogging {
                    events "passed", "skipped", "failed", "standardOut", "standardError"

                    showExceptions true
                    exceptionFormat "full"
                    showCauses true
                    showStackTraces true
                }
            }

            includeAndroidResources true
            returnDefaultValues true // added AURI - Avoid mock errors
        }
    }
    
    //testBuildType "release"

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [
                title: "AegisDev",
                iconName: "ic_launcher_debug",
                fileProviderAuthority: "${fileProviderAuthorityDebug}"
            ]
            buildConfigField("String", "FILE_PROVIDER_AUTHORITY", "\"${fileProviderAuthorityDebug}\"")
            resValue "bool", "pref_secure_screen_default", "false"
        }

        release {
            manifestPlaceholders = [
                title: "Aegis",
                iconName: "ic_launcher",
                fileProviderAuthority: "${fileProviderAuthority}"
            ]
            buildConfigField("String", "FILE_PROVIDER_AUTHORITY", "\"${fileProviderAuthority}\"")
            resValue "bool", "pref_secure_screen_default", "true"

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    
    packagingOptions {
        // R8 doesn't remove these resources, so exclude them manually. This reduces APK size by 4MB.
        resources {
            excludes += ['/org/bouncycastle/pqc/**/*.properties']
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
        coreLibraryDesugaringEnabled true
    }
    lint {
        abortOnError true
        checkDependencies true
        disable 'MissingQuantity', 'MissingTranslation'
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.8.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    def cameraxVersion = '1.2.0'
    def glideVersion = '4.14.2'
    def guavaVersion = '31.1'
    def hiltVersion = '2.44.2'
    def junitVersion = '4.13.2'
    def libsuVersion = '5.0.3'

    annotationProcessor 'androidx.annotation:annotation:1.5.0'
    annotationProcessor "com.google.dagger:hilt-compiler:$hiltVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:${glideVersion}"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.activity:activity:1.6.1'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation "androidx.biometric:biometric:1.1.0"
    implementation "androidx.camera:camera-camera2:$cameraxVersion"
    implementation "androidx.camera:camera-lifecycle:$cameraxVersion"
    implementation "androidx.camera:camera-view:1.1.0"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.core:core:1.9.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation "androidx.lifecycle:lifecycle-process:2.5.1"
    implementation ('androidx.preference:preference:1.2.0') {
        // See: https://issuetracker.google.com/issues/238425626
        exclude group: 'androidx.lifecycle', module:'lifecycle-viewmodel'
        exclude group: 'androidx.lifecycle', module:'lifecycle-viewmodel-ktx'
    }
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    implementation 'com.github.avito-tech:krop:0.52'
    implementation "com.github.bumptech.glide:annotations:${glideVersion}"
    implementation "com.github.bumptech.glide:glide:${glideVersion}"
    implementation("com.github.bumptech.glide:recyclerview-integration:${glideVersion}") {
        transitive = false
    }
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:io:${libsuVersion}"
    implementation "com.google.guava:guava:${guavaVersion}-android"
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'com.google.protobuf:protobuf-javalite:3.21.4'
    implementation 'com.google.zxing:core:3.5.1'
    implementation "com.mikepenz:iconics-core:3.2.5"
    implementation 'com.mikepenz:material-design-iconic-typeface:2.2.0.5@aar'
    implementation 'com.nulab-inc:zxcvbn:1.7.0'
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'de.psdev.licensesdialog:licensesdialog:2.2.0'
    implementation 'net.lingala.zip4j:zip4j:2.11.2'
    implementation 'info.guardianproject.trustedintents:trustedintents:0.2'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.72'
    implementation "org.simpleflatmapper:sfm-csv:8.2.3"


    //implementation "androidx.core:core-ktx:1.6.0"
    //implementation 'androidx.test:runner:1.5.1'
    //implementation "androidx.test.espresso:espresso-core:3.5.0"


    androidTestAnnotationProcessor "com.google.dagger:hilt-android-compiler:$hiltVersion"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hiltVersion"
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.1'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.0'
    androidTestImplementation "junit:junit:${junitVersion}"
    androidTestUtil 'androidx.test:orchestrator:1.4.2'

    testImplementation 'androidx.test:core:1.5.0'
    testImplementation "com.google.guava:guava:${guavaVersion}-jre"
    testImplementation "junit:junit:${junitVersion}"
    testImplementation 'org.json:json:20220924'
    testImplementation 'org.robolectric:robolectric:4.9'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}

configurations.all {
    resolutionStrategy {
        force 'androidx.activity:activity-ktx:1.6.1'
        force 'com.google.auto.value:auto-value-annotations:1.9'
        force 'org.jetbrains:annotations:23.0.0'
        force 'androidx.camera:camera-core:1.2.0'
        force 'androidx.camera:camera-lifecycle:1.2.0'
        force 'androidx.collection:collection:1.1.0'
        force 'androidx.concurrent:concurrent-futures:1.1.0'
        force 'androidx.constraintlayout:constraintlayout:2.1.4'
        force 'androidx.test:core:1.5.0'
        force 'androidx.customview:customview:1.1.0'
        force 'androidx.documentfile:documentfile:1.0.1'
        force 'androidx.drawerlayout:drawerlayout:1.1.1'
        force 'com.google.errorprone:error_prone_annotations:2.11.0'
        force 'androidx.exifinterface:exifinterface:1.3.3'
        force 'com.google.guava:guava:31.1-android'
        force 'org.hamcrest:hamcrest-core:2.2'
        force 'org.hamcrest:hamcrest-library:2.2'
        force 'com.google.code.findbugs:jsr305:3.0.2'
        force 'org.jetbrains.kotlin:kotlin-stdlib-common:1.6.0'
        force 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.0'
        force 'org.jetbrains.kotlin:kotlin-stdlib:1.6.0'
        force 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'
        force 'androidx.lifecycle:lifecycle-livedata:2.1.0'
        force 'androidx.lifecycle:lifecycle-process:2.5.1'
        force 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
        force 'androidx.test:monitor:1.6.0'
        force 'androidx.test:runner:1.5.1'
        force 'androidx.savedstate:savedstate-ktx:1.2.0'
        force 'androidx.startup:startup-runtime:1.1.1'
        force 'androidx.transition:transition:1.4.1'
    }
}
